plugins {
    id("com.gradleup.shadow") version "9.3.0"
    id("io.micronaut.application") version "4.6.1"
    id("io.micronaut.test-resources") version "4.6.1"
    id("io.micronaut.aot") version "4.6.1"
}

version = "2.0.0" // Base version - actual release versions come from git tags
group = "ca.team1310.ravenbrain"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    annotationProcessor("io.micronaut.servlet:micronaut-servlet-processor")
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation("io.micronaut.flyway:micronaut-flyway")

    annotationProcessor 'io.micronaut.data:micronaut-data-processor'
    implementation 'io.micronaut.data:micronaut-data-jdbc'

    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    compileOnly("org.projectlombok:lombok:1.18.36")
    compileOnly("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("com.mysql:mysql-connector-j")
    runtimeOnly("org.flywaydb:flyway-mysql")
    runtimeOnly("org.yaml:snakeyaml")
    testImplementation("io.micronaut:micronaut-http-client")
    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    aotPlugins platform("io.micronaut.platform:micronaut-platform")
    aotPlugins("io.micronaut.security:micronaut-security-aot")
}


application {
    mainClass = "ca.team1310.ravenbrain.Application"
}
java {
    sourceCompatibility = JavaVersion.toVersion("25")
    targetCompatibility = JavaVersion.toVersion("25")
}

// Exclude local development properties from production JAR/Docker only
shadowJar {
    exclude 'application-local.properties'
    exclude 'application-test.properties'
}


graalvmNative.toolchainDetection = false

micronaut {
    runtime("tomcat")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("ca.team1310.ravenbrain.*")
    }
    testResources {
        additionalModules.add("jdbc-mysql")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        replaceLogbackXml = true
        configurationProperties.put("micronaut.security.jwks.enabled", "false")
    }
}

// Configure manifest attributes for all JAR tasks
def manifestAttributes = [
    'Implementation-Title': 'Runnymede Robotics Team 1310 Raven Brain',
    'Implementation-Version': project.version,
    'Implementation-Vendor': project.group
]

jar {
    manifest {
        attributes(manifestAttributes)
    }
}

// Configure the runner JAR used by Micronaut Docker build
tasks.named('runnerJar', Jar) {
    manifest {
        attributes(manifestAttributes)
    }
}
tasks.register('clearCdsClasslist') {
    doFirst {
        File classlist = file("build/test-resources/cds/cds.classlist")
        if (classlist.exists()) {
            println "Applying workaround to clear cds.classlist"
            classlist.delete()
        }
    }
}

// Ensure it runs before any other task
tasks.configureEach { task ->
    if (task.name != 'clearCdsClasslist') {
        task.dependsOn 'clearCdsClasslist'
    }
}

// Docker image configuration
dockerfile {
    baseImage = "eclipse-temurin:25-jre"
    exposedPorts = [8888]
}

dockerBuild {
    images = ["ravenbrain:${project.version}", "ravenbrain:latest"]
}

// Docker deployment tasks
tasks.register('deployDocker', Exec) {
    group = 'deployment'
    description = 'Builds the Docker image and starts containers via docker-compose'
    dependsOn 'dockerBuild'
    commandLine 'docker-compose', 'up', '-d'
}

tasks.register('stopDocker', Exec) {
    group = 'deployment'
    description = 'Stops and removes Docker containers'
    commandLine 'docker-compose', 'down'
}

tasks.register('deployDockerProd', Exec) {
    group = 'deployment'
    description = 'Locally test the production Docker configuration (not for actual server deployment)'
    dependsOn 'dockerBuild'
    commandLine 'docker-compose', '-f', 'docker-compose.yml', '-f', 'docker-compose.prod.yml', 'up', '-d'
}